/*
 * Java Genetic Algorithm Library (@__identifier__@).
 * Copyright (c) @__year__@ Franz Wilhelmstötter
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Author:
 *    Franz Wilhelmstötter (franz.wilhelmstoetter@gmx.at)
 */
/**
 * @author <a href="mailto:franz.wilhelmstoetter@gmx.at">Franz Wilhelmstötter</a>
 * @since 1.2
 * @version 2.0 &mdash; <em>$Date$</em>
 */

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'packaging'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
	mavenCentral ()
}

dependencies {
	testCompile Include.Apache.Commons.Math
	testCompile Include.TestNG
}

jar.manifest.instruction('Export-Package',
	'org.jenetics',
	'org.jenetics.util',
	'org.jenetics.stat'
)

packaging {
	name = 'Jenetics'
	author = 'Franz Wilhelmstötter'
	url = 'http://jenetics.sourceforge.net'

    jarjar = false
}

task sourceJar(type: Jar) {
	classifier "sources"
	from sourceSets.main.allSource
    filter(ReplaceTokens, tokens: [
        __identifier__: identifier,
        __year__: copyrightYear
    ])
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
    filter(ReplaceTokens, tokens: [
        __identifier__: identifier,
        __year__: copyrightYear
    ])
}

ext {
    pomFilePath = "${project.projectDir}/tmp/pom.xml"
    pomFile = file(pomFilePath)
}

configurations {
    pom
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
    if (pomFile.exists()) {
        pom pomFile
    }
}

task signJars (type : Sign, dependsOn: [jar, javadocJar, sourceJar]) {
    sign configurations.archives
}

task signPom(type: Sign) {
    sign configurations.pom
}

if (project.ext.pomFile.exists()) {
    task preparePublication (dependsOn : [signJars, signPom])
} else {
    task preparePublication (dependsOn : signJars)
}


def getSignatureFiles = {
    def allFiles = project.tasks.signJars.signatureFiles.collect { it }
    def signedSources = allFiles.find { it.name.contains('-sources') }
    def signedJavadoc = allFiles.find { it.name.contains('-javadoc') }
    def signedJar = (allFiles - [signedSources, signedJavadoc])[0]
    return [
            [archive: signedSources, classifier: 'sources', extension: 'jar.asc'],
            [archive: signedJavadoc, classifier: 'javadoc', extension: 'jar.asc'],
            [archive: signedJar,     classifier: null,      extension: 'jar.asc']
    ]
}

def getPomSignature = {
    return project.tasks.signPom.signatureFiles.collect{it}[0]
}

publishing {
    publications {
        gpgJars(MavenPublication) {
            getSignatureFiles().each {signature ->
                artifact (signature.archive) {
                    classifier = signature.classifier
                    extension  = signature.extension
                }
            }
        }
        if (project.ext.pomFile.exists()) {
            gpgPom(MavenPublication) {
                artifact (getPomSignature()) {
                    classifier = null
                    extension  = 'pom.asc'
                }
            }
        }
        jar(MavenPublication) {
            from components.java
            artifact (javadocJar) {
                classifier = 'javadoc'
            }
            artifact (sourceJar) {
                classifier = 'sources'
            }

            pom.withXml { asNode().children().last() + {
                resolveStrategy = Closure.DELEGATE_FIRST
                name jenetics.name
                description jenetics.description
                url jenetics.url
                packaging 'jar'
                scm {
                    url jenetics.maven.scm.url
                    connection jenetics.maven.scm.connection
                    developerConnection jenetics.maven.scm.developer_connection
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'fwilhelm'
                        name 'Franz Wilhelmstötter'
                        email 'franz.wilhelmstoetter@gmx.at'
                    }
                }
            }}
        }
    }

	repositories {
        maven {
            //if (project.ext.pomFile.exists()) {
                url 'https://oss.sonatype.org/content/repositories/snapshots'
                //url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username = project.hasProperty('sonatype_username') ? sonatype_username : 'foo'
                    password = project.hasProperty('sonatype_password') ? sonatype_password : 'bar'
                }
            //} else {
            //    url "$buildDir/repo"
            //}
        }
	}
}

publish.dependsOn(preparePublication)




