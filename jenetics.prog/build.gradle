/*
 * Java Genetic Algorithm Library (@__identifier__@).
 * Copyright (c) @__year__@ Franz Wilhelmstötter
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 2.1 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.	See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
 *
 * Author:
 *   Franz Wilhelmstötter (franz.wilhelmstoetter@gmail.com)
 *
 */

/**
 * @author <a href="mailto:franz.wilhelmstoetter@gmail.com">Franz Wilhelmstötter</a>
 * @since 3.9
 * @version 4.4
 */

apply plugin: 'java-library'
apply plugin: 'packaging'
apply plugin: 'nexus'

dependencies {
	api project(':jenetics')
	api project(':jenetics.ext')

	testImplementation property('include.TestNG')
	testImplementation property('include.EqualsVerifier')
}

jar.manifest.attributes('Automatic-Module-Name': 'io.jenetics.prog')

javadoc {
	options {
		links 'http://jenetics.io/javadoc/jenetics',
			'http://jenetics.io/javadoc/jenetics.ext'
	}
}

packaging {
	name = property('jenetics.prog.Name')
	author = property('jenetics.Author')
	url = property('jenetics.Url')

	jarjar = false
	javadoc = true
}

modifyPom {
	project {
		name 'jentics.prog'
		description 'Jenetics Genetic Programming Module'
		url project.property('jenetics.Url')
		inceptionYear '2017'

		scm {
			url project.property('jenetics.MavenScmUrl')
			connection project.property('jenetics.MavenScmConnection')
			developerConnection project.property('jenetics.MavenScmDeveloperConnection')
		}

		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}

		developers {
			developer {
				id project.property('jenetics.Id')
				name project.property('jenetics.Author')
				email project.property('jenetics.Email')
			}
		}
	}
}

nexus {
	identifier = project.identifier
	copyrightYear = project.copyrightYear
	attachSources = true
	attachTests = false
	attachJavadoc = true
	sign = true
	repository = project.property('build.MavenRepository')
	snapshotRepository = project.property('build.MavenSnapshotRepository')
}



