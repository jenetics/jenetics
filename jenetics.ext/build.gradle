/*
 * Java Genetic Algorithm Library (@__identifier__@).
 * Copyright (c) @__year__@ Franz Wilhelmstötter
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Author:
 *    Franz Wilhelmstötter (franz.wilhelmstoetter@gmail.com)
 */

/**
 * @author <a href="mailto:franz.wilhelmstoetter@gmail.com">Franz Wilhelmstötter</a>
 * @since 3.5
 * @version 4.4
 */

plugins {
	id 'me.champeau.gradle.jmh' version '0.5.0-rc-2'
}

apply plugin: 'java-library'
apply plugin: 'packaging'
apply plugin: 'nexus'

description = 'Jenetics extension - Java Genetic Algorithm Library Extension'

compileTestJava.dependsOn tasks.getByPath(':jenetics:compileTestJava')

dependencies {
	api project(':jenetics')

	testImplementation property('include.ApacheCommonsMath')
	testImplementation property('include.TestNG')
	testImplementation property('include.EqualsVerifier')
	testImplementation project(':jenetics').sourceSets.test.output
}

jmh {
	//jmhVersion = '1.20'
	//duplicateClassesStrategy = 'include'
	warmupIterations = 2
	iterations = 4
	fork = 1
	include = ['.*TreePerf.*']
}

idea {
	module{
		scopes.COMPILE.plus += [configurations.jmh]
	}
}

jar.manifest.attributes('Automatic-Module-Name': 'io.jenetics.ext')

javadoc {
	options {
		links 'http://jenetics.io/javadoc/jenetics'
	}
}

packaging {
	name = property('jenetics.ext.Name')
	author = property('jenetics.Author')
	url = property('jenetics.Url')

	jarjar = false
	javadoc = true
}

modifyPom {
	project {
		name 'jenetics.ext'
		description 'Jenetics Extension Library'
		url project.property('jenetics.Url')
		inceptionYear '2015'

		scm {
			url project.property('jenetics.MavenScmUrl')
			connection project.property('jenetics.MavenScmConnection')
			developerConnection project.property('jenetics.MavenScmDeveloperConnection')
		}

		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}

		developers {
			developer {
				id project.property('jenetics.Id')
				name project.property('jenetics.Author')
				email project.property('jenetics.Email')
			}
		}
	}
}

nexus {
	identifier = project.identifier
	copyrightYear = project.copyrightYear
	attachSources = true
	attachTests = false
	attachJavadoc = true
	sign = true
	repository = project.property('build.MavenRepository')
	snapshotRepository = project.property('build.MavenSnapshotRepository')
}
