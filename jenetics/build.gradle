/*
 * Java Genetic Algorithm Library (@__identifier__@).
 * Copyright (c) @__year__@ Franz Wilhelmstötter
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Author:
 *    Franz Wilhelmstötter (franz.wilhelmstoetter@gmail.com)
 */

/**
 * @author <a href="mailto:franz.wilhelmstoetter@gmail.com">Franz Wilhelmstötter</a>
 * @since 1.2
 * @version 4.3
 */

plugins {
	id 'me.champeau.gradle.jmh' version '0.4.5'
}

apply plugin: 'packaging'
apply plugin: 'nexus'

description = property('jenetics.Description')

repositories {
	mavenCentral()
	jcenter()
}

dependencies {
	testCompile property('include.ApacheCommonsMath')
	testCompile property('include.TestNG')
	testCompile property('include.EqualsVerifier')
	testCompile property('include.PRNGine')

	jmh property('include.PRNGine')
}

jmh {
	//jmhVersion = '1.20'
	//duplicateClassesStrategy = 'include'
	warmupIterations = 5
	iterations = 5
	include = ['.*ThreadIDPerf.*']
}

idea {
	module{
		scopes.COMPILE.plus += [configurations.jmh]
	}
}

jar.manifest.instruction('Export-Package',
	'io.jenetics',
	'io.jenetics.engine',
	'io.jenetics.util',
	'io.jenetics.stat'
)

jar.manifest.attributes('Automatic-Module-Name': 'io.jenetics.base')

test.dependsOn(compileJmhJava)

packaging {
	name = property('jenetics.Name')
	author = property('jenetics.Author')
	url = property('jenetics.Url')

	jarjar = false
	javadoc = true
}

modifyPom {
	project {
		name 'jenetics'
		description project('jenetics.Description')
		url jenetics.url
		inceptionYear '2007'

		scm {
			url property('jenetics.MavenScmUrl')
			connection property('jenetics.MavenScmConnection')
			developerConnection property('jenetics.MavenScmDeveloperConnection')
		}

		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}

		developers {
			developer {
				id property('jenetics.Id')
				name property('jenetics.Author')
				email property('jenetics.Email')
			}
		}
	}
}

nexus {
	identifier = project.identifier
	copyrightYear = project.copyrightYear
	attachSources = true
	attachTests = false
	attachJavadoc = true
	sign = true
	repository = property('build.MavenRepository')
	snapshotRepository = property('build.MavenSnapshotRepository')
}
